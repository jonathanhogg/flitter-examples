-- Genuary day 29: Signed Distance Functions
--
-- (Also Genuary day 12: Lava lamp; and Genuary day 30: Shaders)
--
-- This sketch makes use of Flitter's support for custom shaders in 3D scenes.
--
-- A simple physics system simulates a bunch of blobs with random radii,
-- starting positions and starting temperatures. Blobs hotter than 1500Â°K rise
-- and cooler blobs sink. Blobs gain or lose heat depending on whether they are
-- nearer to the bottom or top of the sphere. The blobs have opposing collision
-- and sticky forces that make them tend to loosely clump together, some slight
-- Brownian motion randomness and a heavy drag complete the simulation.
--
-- A group is constructed containing a single sphere that is rendered with a
-- custom shader. The custom shader takes the positions, radii and colours of
-- the blobs as input and does raymarching through each fragment with an SDF
-- consisting of a smooth union of all of the blobs. The calculated position,
-- normal and colour of each fragment are passed through the standard PBR
-- lighting code.
--
-- I hope you have a good GPU.
--
-- Copyright 2024 by Jonathan Hogg and licensed under CC BY-NC-SA 4.0
--
-- Record a video with:
-- flitter day29.fl --lockstep --runtime=120 --define 'RECORD=1080'

import bloom_filter from '../common/filters.fl'

let SIZE=RECORD or 1080
    NBLOBS=100

let t=$(:blob;:clock)
    blob_start_temp=1000+1000*beta(:radius)[..NBLOBS]
    blob_radii=5*(0.5+beta(:radius)[..NBLOBS])

func blob_color(i)
    let temperature=blob_start_temp[i]+counter(:temp;i, t)
    colortemp(temperature)*500

!physics dimensions=3 state=:blob resolution=1/20
    !collision strength=2
    !sticky strength=1
    !random strength=1
    !drag strength=25m
    !anchor id=:middle
    for i in ..NBLOBS
        let temperature=blob_start_temp[i]+counter(:temp;i, t, -5*$(:blob;i)[1]/blob_radii[i]**2)
        !particle id=i radius=blob_radii[i] mass=blob_radii[i]**3 position=normal(:pos;i)[..3]*10 force=0;(temperature-1500)/100;0
        !distance from=:middle to=i max=50 strength=50

!window size=SIZE
    @bloom_filter id=:top radius=SIZE[0]/54
        !canvas3d viewpoint=0;0;100 far=200 fov=80/360 fog_color=colortemp(1500) fog_max=200 fog_curve=2
            !light color=colortemp(3000)*100k direction=0;1;0 position=0;-100;0
            !material roughness=0.35 metal=true color=0.5
                !group fragment=read('../glsl/sdf_shader.frag') \
                       NSPHERES=NBLOBS smoothing=10 max_iterations=50 normal_delta=0.1 epsilon=0.01 far=200 \
                       sphere_positions=($(:blob;i) for i in ..NBLOBS) \
                       sphere_colors=(blob_color(i) for i in ..NBLOBS) \
                       sphere_radii=blob_radii
                    !sphere size=60
    !record filename=('day29.mp4' if RECORD and t > 60) codec=:hevc crf=25 hidden=true
        !reference id=:top
