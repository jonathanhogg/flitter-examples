-- Genuary day 16: Draw 10,000 of something
--
-- Lots of interesting patterns fall out of drawing very large numbers of
-- overlapping objects. Here 10,000 tetrahedrons are drawn in a twisted torus
-- arrangement. As so many objects are being rendered, they become an almost
-- continuous, sinuous form.
--
-- Note the hack here for getting a tetrahedron: drawing a cone with only
-- 3 segments and using flat shading. Cone primitives are constructed as
-- `segments` triangles forming the sides and the same number of triangles
-- forming the base. The sides are normally rendered with smooth shading, where
-- the surface normal is varied across the surface of each triangle. With
-- sufficient segments (the default is 32) the result will look like a smooth
-- cone. Enabling `flat` shading will turn off the normal smooth shading of the
-- sides so that each triangle has a single unchanging surface normal.
--
-- With drawing so many objects here, packed so closely together, better visual
-- results are actually obtained with flat shading. Reducing the number of
-- objects, and playing with increasing the number of cone segments and using
-- smooth shading produces lots of different interesting ribbed surfaces.
--
-- Copyright 2024 by Jonathan Hogg and licensed under CC BY-NC-SA 4.0
--
-- Record a video with:
-- flitter day16.fl --lockstep --runtime=60 --fps=30 --define 'RECORD=1080'

%pragma tempo 60

let SIZE=RECORD or 1080
    M=20
    N=10k

!window size=SIZE
    !bloom id=:top radius=hypot(SIZE[..2])/100
        !canvas3d samples=8 viewpoint=0;0;100 fov=40/360 rotate=beat/(30;20;15)
            !light color=1 direction=1;0;0
            !light color=1 direction=0;1;0
            !light color=1 direction=0;0;1
            !material color=0.944;0.776;0.373 metal=1 roughness=.2
                for i in ..N
                    !transform rotate_y=i/N translate=20;0;0 rotate_z=i*M/N
                        !cone segments=3 flat=true position=7.5;0;0 size=3 rotation=-0.5/M+1/N;0;1/12
    !record filename=('day16.mp4' if RECORD) codec=:hevc crf=25 hidden=true
        !reference id=:top
