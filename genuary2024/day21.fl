-- Genuary day 21: Use a library that you havenâ€™t used before
--
-- This makes use of the [manifold3d](m3d) library to construct a classic
-- flying saucer shape (general shape inspired by the film 'Mars Attacks!') out
-- of only primitive spheres, boxes, cylinders and cones.
--
-- [m3d]: https://github.com/elalish/manifold
--
-- Copyright 2024 by Jonathan Hogg and licensed under CC BY-NC-SA 4.0
--
-- Record a video with:
-- flitter day21.fl --lockstep --runtime=30 --fps=60 --define 'RECORD=1080'


%pragma tempo 60

import bloom_filter from '../common/filters.fl'

let SIZE=RECORD or 1080
    NLEGS=6
    t=beat


func saucer(_, segments=128)
    !intersect
        !sphere segments=segments size=1 position=0;0;-0.9
        !sphere segments=segments size=1.5 position=0;0;1.4

func leg(_)
    !difference
        !union
            !box size=0.2;0.01;0.04 position=0.1;0;0
            !cylinder size=0.02;0.02;0.01 position=0.2;0;0 rotation=0.25;0;0
            !transform translate=0.2;0;0 rotate_y=0.08
                !difference
                    !box size=0.1;0.01;0.04 position=0.05;0;0
                    !transform rotate_y=0.01
                        !box size=0.2;0.01;0.04 position=0.05;0;0.04
                !cylinder size=0.017;0.017;0.01 position=0.1;0;-0.003 rotation=0.25;0;0
                !transform translate=0.1;0;-0.003 rotate_y=0.03
                    !difference
                        !box size=0.1;0.01;0.032 position=0.05;0;0
                        !transform rotate_y=0.01
                            !box size=0.15;0.01;0.032 position=0.05;0;0.032
                    !cylinder size=0.013;0.013;0.01 position=0.1;0;-0.003 rotation=0.25;0;0
                    !transform translate=0.1;0;0 rotate_y=0.1
                        !difference
                            !box size=0.1;0.01;0.02 position=0.05;0;0
                            !transform rotate_y=0.02
                                !box size=0.15;0.01;0.02 position=0.05;0;0.02
                        !cylinder size=0.005;0.005;0.01 position=0.1;0;-0.006 rotation=0.25;0;0
        for j in ..4
            !cylinder size=0.015;0.015;0.02 rotation=0.25;0;0 position=0.2*(j+1)/4.75;0;0
        !transform translate=0.2;0;0 rotate_y=0.085
            for j in ..3
                !cylinder size=(0.013;0.013)-0.001*j;0.02 rotation=0.25;0;0 position=0.11*(j+0.25)/3;0;0
            !transform translate=0.1;0;0 rotate_y=0.03
                for j in ..3
                    !cylinder size=(0.011;0.011)-0.001*j;0.02 rotation=0.25;0;0 position=0.1*(j+0.5)/3;0;0

func laser(_, power=0, angle=0.125)
    !cylinder start=0 end=0;0;-0.05 radius=0.005
    !transform translate=0;0;-0.05 rotate_x=angle
        !sphere position=0 size=0.008
        !cylinder start=0 end=0;0;-0.1 radius=0.005
        !cone position=0;0;-0.1 size=0.02
        if power
            !material color=0 metal=false roughness=1
                !cylinder start=0;0;-0.1 end=0;0;-2000 radius=0.002 emissive=0;linear(power);0
                !cylinder start=0;0;-0.1 end=0;0;-2000 radius=0.005 emissive=power;0;0 transparency=0.5


!window size=SIZE
    @bloom_filter id=:top radius=hypot(SIZE)/80 exposure=-1
        !canvas3d viewpoint=0;0;500 far=2000 fov=30/360 samples=4 fog_min=500 fog_max=2000
            !light color=0.25 direction=0;0;-1
            !light color=0.25 direction=0;-1;0
            !light color=0.25 direction=1;1;1
            !transform rotate=-0.25+sin(t/15)*0.05;sin(t/10)*0.03;-beat/30
                !transform scale=200 translate=0;0;0.05
                    !material color=0.5 metal=true roughness=0.4
                        !union smooth=0.06 minarea=1/1000
                            !sphere segments=64 size=0.16;0.16;0.13 position=0;0;0.05
                            !difference
                                @saucer
                                for i in ..4
                                    !transform rotate_z=i/4 translate=0.5;0;0.05
                                        !box size=0.6;0.005;0.1
                            !transform scale=0.98
                                @saucer
                        !transform translate=0;0;-0.12
                            !difference
                                !sphere size=0.12;0.12;0.09 position=0;0;0.05
                                !cylinder size=0.02;0.02;0.1
                                for i in ..NLEGS
                                    !transform rotate_z=i/NLEGS
                                        !box size=0.2;0.03;0.05 position=0.1;0;0
                            for i in ..NLEGS
                                !transform rotate_z=i/NLEGS rotate_y=-0.01 translate=0.05;0;-0.006
                                    @leg
                        !material color=0 metal=false roughness=1
                            !transform translate=0;0;-0.07
                                !sphere size=0.08
                        !transform translate=0;0;-0.15 rotate_z=-beat/10
                            @laser power=0.5+4.5*impulse(beat*5) angle=0.1+sine(beat/5)*0.1
    !record filename=('day21.mp4' if RECORD) codec=:hevc crf=25 hidden=true
        !reference id=:top
