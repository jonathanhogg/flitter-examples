-- Genuary day 9: ASCII
--
-- This is an homage to my first computer, the BBC Micro. This uses the original
-- BBC Micro font, although sticklers will note that the BBC actually boots into
-- MODE 7, which uses a chip-based character generator (same one as the old
-- Teletext service) not the bitmap font.
--
-- Copyright 2024 by Jonathan Hogg and licensed under CC BY-NC-SA 4.0
--
-- Record a video with:
-- flitter day9.fl --lockstep --runtime=15 --fps=60 --define 'RECORD=1080'

import bloom_filter from '../common/filters.fl'

let SIZE=RECORD or 1080
    TEXT='BBC Computer 32K';'Acorn DFS';'BASIC';'>10 PRINT"HELLO!"';'>20 GOTO 10';'>RUN_'
    BBC_FONT=read_bytes('../data/bbc')

func char_bitmap(char)
    let offset=(char-32)*8
    for i in ..8
        BBC_FONT[offset+i] // 2**(7..-1|-1) % 2

!window size=SIZE colorbits=16
    @bloom_filter id=:top radius=hypot(SIZE)/60
        !canvas3d samples=4 viewpoint=0;0;80 focus=0;0;0 near=1 far=160 fov=100/360 \
                  fog_color=0 fog_min=80 fog_max=160
            !light color=2 direction=0;0;-1
            !transform rotate=beat/30 translate=-64;32;0
                !material shininess=100
                    let m=len(TEXT)
                    for i in ..m
                        let characters=ord(TEXT[i])
                            n=len(characters)
                        for j in ..n
                            let bitmap=char_bitmap(characters[j])
                            if i != m-1 or j != n-1 or beat//1%2 == 0
                                for k in ..64
                                    if bitmap[k]
                                        !transform translate=j*8+k%8;-i*8-k//8;0
                                            !cylinder size=1/6;0.4;0.8 emissive=1;0;0 position=-1/3;0;0
                                            !cylinder size=1/6;0.4;0.8 emissive=0;1;0
                                            !cylinder size=1/6;0.4;0.8 emissive=0;0;1 position=1/3;0;0
    !record filename=('day9.mp4' if RECORD) codec=:hevc crf=20 hidden=true
        !reference id=:top
