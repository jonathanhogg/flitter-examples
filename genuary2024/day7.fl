-- Genuary day 7: Progress bar / loading screen
--
-- This is a rough experiment in ragdoll physics. It doesn't work very well
-- really, because the Flitter physics engine is very simplistic.
--
-- Record a video with:
-- flitter day7.fl --lockstep --runtime=70 --define 'RECORD=1080'

%pragma tempo 60

let SIZE=RECORD or 1080
    DOLLS=120
    END=min(DOLLS, beat//1)
    START=max(0, END-DOLLS)
    SEGMENTS=32
    TUBE=20
    JOINTS=(:head;4; :spine_top;2; :left_shoulder;2; :right_shoulder;2; :left_elbow;2; :right_elbow;2; :left_hand;2; :right_hand;2;
            :spine_bottom;3; :left_hip;2; :right_hip;2; :left_knee;2; :right_knee;2; :left_foot;2; :right_foot;2)
    BONES=(:head;:spine_top;3;2; :spine_top;:left_shoulder;3;2; :spine_top;:right_shoulder;3;2; :left_shoulder;:left_elbow;4;2; :right_shoulder;:right_elbow;4;2;
           :left_elbow;:left_hand;4;2; :right_elbow;:right_hand;4;2; :spine_top;:spine_bottom;8;3; :spine_bottom;:left_hip;2;2; :spine_bottom;:right_hip;2;2;
           :left_hip;:left_knee;4;2; :right_hip;:right_knee;4;2; :left_knee;:left_foot;4;2; :right_knee;:right_foot;4;2)

!physics state=:doll dimensions=3
    for i in START..END
        let age=beat-i
            start=(beta(:start;i)[..3]-0.5)*TUBE*2 + (0;0;500)
        for id;r in JOINTS
            !particle id=i;id radius=r charge=r mass=r position=start+uniform(i;id)[..3]*10
        for from;to;d;r in BONES
            !distance from=i;from to=i;to fixed=d strength=map(linear(age/10), 500, 1k)
    for i in ..SEGMENTS
        let x;y=polar(i/SEGMENTS)
        !barrier position=(TUBE-1)*(x;y);0 normal=-(x;y);0 restitution=0.5
    !barrier position=0;0;0 normal=0;0;1 restitution=0.1
    !constant acceleration=0;0;-50
    !electrostatic strength=10
    !collision strength=1k
    !drag strength=0.004

!window size=SIZE colorbits=16
    !canvas3d id=:top samples=4 viewpoint=0;-300;100 focus=0;0;100 up=0;0;1 near=1 far=500 fov=60/360 fog_color=0 fog_min=300 fog_max=500
        !light color=0.25
        !light color=0.75 direction=1;1;0
        !material color=0;1;0
            for i in START..END
                if $(:doll;i;:head)
                    for id;r in JOINTS
                        !sphere position=$(:doll;i;id) size=r
                    for from;to;d;r in BONES
                        !cylinder start=$(:doll;i;from) end=$(:doll;i;to) radius=r
        !material color=0.2 transparency=0.9 shininess=100
            !transform
                !cylinder position=0;0;100 size=TUBE-1;TUBE-1;200 invert=true
                !cylinder position=TUBE;TUBE;200
        !material color=0.1
            !cylinder position=0;0;-0.5 size=500;500;1
    !record filename=('day7.mp4' if RECORD and beat>10) limit=60 codec=:hevc crf=20 hidden=true
        !reference id=:top
