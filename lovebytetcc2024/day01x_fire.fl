
-- LoveByte Tiny Code Christmas 2024
-- Day 1 extra: Fire
--
-- The normal fire demo effect requires state. Here I'm using `!counter` objects to
-- provide that state. There is a counter per column with the rate of change of the
-- bottom layer being some input noise and the rate of each layer above dependent
-- on the average of the neighbouring spheres in the layer below.


let N=30 W=time T=W*10 O=-1..2 R=..N

for x in R,z in R
    !counter state=x;z min=0 max=1 time=T initial=0[R] rate=noise(0,x,z,W);2*mean($(x+o;z+p) for o in O for p in O,N)[..N-1]-1.03

!window size=1080
    !canvas3d id=:t position=N/2 composite=:add depth_test=0 rotate_y=W/20
        (!sphere position=(x;y;z)-N/2 size=.45 emissive=normalize(colortemp($(x;z)[y]*4k))) if $(x;z)[y] for x in R for y in R for z in R


    -- Record a short looping video with:
    -- flitter lovebytetcc2024/day01x_fire.fl --lockstep --runtime=30

    if run_time and frame > 1
        !record filename='day01x_fire.mp4' codec=:hevc crf=30
            !reference id=:t
