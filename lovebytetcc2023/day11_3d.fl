-- The Third Dimension
--
-- This example is based on code written for a live performance of Feral
-- Five's song "Ego". A 3D slice through 4D noise is used to derive the size
-- of each box with the 4th axis moving continuously to create the shifting
-- dark clouds effect. A single very bright light is placed at the centre of
-- the voxel cube.
--
-- Copyright 2024 by Jonathan Hogg and licensed under CC BY-NC-SA 4.0

let N=25 D=..N G=(.5+D)/N-.5 R=G*3 S=1k P=G*S T=time f=quad(noise(0,T,R,R,R)+.5)*S/N
!window size=1080
    !canvas3d position=S far=2*S fov=1/6 color=1 rotate=T/30
        !light position=0 color=.2M
        for i in D, j in D, k in D
            !box position=P[i;j;k] size=f[(i*N+j)*N+k]
